# - Find Windows Platform SDK
# Find the Windows includes
#
#  WINSDK_INCLUDE_DIR - where to find Windows.h
#  WINSDK_FOUND       - True if Windows SDK found.

IF(WINSDK_INCLUDE_DIR)
    # Already in cache, be silent
    SET(WINSDK_FIND_QUIETLY TRUE)
ENDIF(WINSDK_INCLUDE_DIR)

GET_FILENAME_COMPONENT(WINSDK71_DIR  "[HKEY_LOCAL_MACHINE\\SOFTWARE\\Microsoft\\Microsoft SDKs\\Windows\\v7.1;InstallationFolder]" ABSOLUTE CACHE)
GET_FILENAME_COMPONENT(WINSDK71_VERSION "[HKEY_LOCAL_MACHINE\\SOFTWARE\\Microsoft\\Microsoft SDKs\\Windows\\v7.1;ProductVersion]" NAME)

IF(WINSDK71_DIR)
  IF(NOT WINSDK_FIND_QUIETLY)
    MESSAGE(STATUS "Found Windows SDK ${WINSDK71_VERSION} in ${WINSDK71_DIR}")
  ENDIF(NOT WINSDK_FIND_QUIETLY)
ENDIF(WINSDK71_DIR)

GET_FILENAME_COMPONENT(WINSDKCURRENT_DIR  "[HKEY_LOCAL_MACHINE\\SOFTWARE\\Microsoft\\Microsoft SDKs\\Windows;CurrentInstallFolder]" ABSOLUTE CACHE)
GET_FILENAME_COMPONENT(WINSDKCURRENT_VERSION "[HKEY_LOCAL_MACHINE\\SOFTWARE\\Microsoft\\Microsoft SDKs\\Windows;CurrentVersion]" NAME)

IF(WINSDKCURRENT_DIR)
  IF(NOT WINSDK_FIND_QUIETLY)
    MESSAGE(STATUS "Found Windows SDK ${WINSDKCURRENT_VERSION} in ${WINSDKCURRENT_DIR}")
  ENDIF(NOT WINSDK_FIND_QUIETLY)
ENDIF(WINSDKCURRENT_DIR)

FIND_PATH(WINSDK_INCLUDE_DIR Windows.h
  PATHS
  ${WINSDK71_DIR}/Include
  ${WINSDKCURRENT_DIR}/Include
)

FIND_PROGRAM(WINSDK_SIGNTOOL signtool
  PATHS
  ${WINSDK71_DIR}/Bin
  ${WINSDKCURRENT_DIR}/Bin
)

IF(WINSDK_INCLUDE_DIR)
  SET(WINSDK_FOUND TRUE)
ELSE(WINSDK_INCLUDE_DIR)
  IF(NOT WINSDK_FIND_QUIETLY)
    MESSAGE(STATUS "Warning: Unable to find Windows SDK!")
  ENDIF(NOT WINSDK_FIND_QUIETLY)
ENDIF(WINSDK_INCLUDE_DIR)
